@echo off
setlocal ENABLEEXTENSIONS ENABLEDELAYEDEXPANSION

set "ROOTDIR=%~dp0"

set CONFIGURATION=%1
set PLATFORM=%2
set BUILD_CONFIG=!CONFIGURATION!
set PLATFORM_TARGET=!PLATFORM!

if "!CONFIGURATION!"=="ReleaseMT" (
	set BUILD_CONFIG=Release
)

if "%PLATFORM%"=="x64" (
set "TOOL_SET=Visual Studio 15 2017 Win64" 
) else (
set "TOOL_SET=Visual Studio 15 2017" 
)

if "!CONFIGURATION!"=="ReleaseMT" (

set "CMAKE_C_FLAGS_DEBUG=/MTd /Zi /Od /Ob0 /RTC1 /D_DEBUG"
set "CMAKE_C_FLAGS_MINSIZEREL=/MT  /O1 /Ob1 /DNDEBUG"
set "CMAKE_C_FLAGS_RELEASE=/MT  /O2 /Ob2 /DNDEBUG"
set "CMAKE_C_FLAGS_RELWITHDEBINFO=/MT /Zi /O2 /Ob1 /DNDEBUG"

) else (
set "CMAKE_C_FLAGS_DEBUG=/MDd /Zi /Od /Ob0 /RTC1 /D_DEBUG"
set "CMAKE_C_FLAGS_MINSIZEREL=/MD  /O1 /Ob1 /DNDEBUG"
set "CMAKE_C_FLAGS_RELEASE=/MD /O2 /Ob2 /DNDEBUG"
set "CMAKE_C_FLAGS_RELWITHDEBINFO=/MD /Zi /O2 /Ob1 /DNDEBUG"
)

set "BUILDDIR=!ROOTDIR!..\3rdparty\lzo\build\!PLATFORM!"

if exist "!ROOTDIR!..\3rdparty\lib\!PLATFORM!\!CONFIGURATION!\lzo2.lib" goto :END

call :DOMKDIR "!ROOTDIR!..\3rdparty\lzo\build"
if errorlevel 1 exit /B 1

call :DOMKDIR "!BUILDDIR!"
if errorlevel 1 exit /B 2

cd "!BUILDDIR!"
if errorlevel 1 exit /B 3

set "CMAKE_INSTALL_PREFIX=.."
set "INSTALL_LIB_DIR=!ROOTDIR!..\3rdparty\lib\!PLATFORM!\!CONFIGURATION!"
set "INSTALL_BIN_DIR=!ROOTDIR!..\3rdparty\bin\!PLATFORM!\!CONFIGURATION!"
set "INSTALL_INC_DIR=!ROOTDIR!..\3rdparty\include"

cmake -G "!TOOL_SET!" ^
-DENABLE_SHARED="ON" ^
-DCMAKE_INSTALL_PREFIX="!CMAKE_INSTALL_PREFIX!" ^
-DCMAKE_INSTALL_LIBDIR="!INSTALL_LIB_DIR!" ^
-DCMAKE_INSTALL_BINDIR="!INSTALL_BIN_DIR!" ^
-DCMAKE_INSTALL_INCLUDEDIR="!INSTALL_INC_DIR!" ^
-DCMAKE_C_FLAGS_DEBUG="!CMAKE_C_FLAGS_DEBUG!" ^
-DCMAKE_C_FLAGS_MINSIZEREL="!CMAKE_C_FLAGS_MINSIZEREL!" ^
-DCMAKE_C_FLAGS_RELEASE="!CMAKE_C_FLAGS_RELEASE!" ^
-DCMAKE_C_FLAGS_RELWITHDEBINFO="!CMAKE_C_FLAGS_RELWITHDEBINFO!" ^
-DAMD64=1 "!ROOTDIR!..\3rdparty\lzo"

if errorlevel 1  exit /B 4


cmake --build . --config !BUILD_CONFIG! --target INSTALL -- /maxcpucount:4
if errorlevel 1  exit /B 4

goto :END


:DOMKDIR
if not exist "%~1" (
	mkdir "%~1"
	if errorlevel 1 exit /b 99
)

goto :EOF


:END
endlocal
